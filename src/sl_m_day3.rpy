init:
    $ sl_m_day3_help = False
    
label slavyana_mod__day3:
    pause (3)
    $ backdrop = "days"
    $ new_chapter(3, u"Славя. День третий")
    $ sl_m_try = "savename3"
    call sl_m_try
    $ sl_m_try = None
    $ day_time()
    $ persistent.sprite_time = "day"
    
#утро
    show unblink
    play ambience ambience_int_cabin_day fadein 3
    scene bg int_house_of_sl_day with dissolve
    pause (3)
    window show
    "Утро наступило быстро и неожиданно."
    "Казалось, что я только-только легла в кровать, моргнула, и вдруг резко стало светло, а трели кузнечиков за окном столь же скоро сменились весёлым щебетанием беззаботных пташек."
    th "Благо, не проспала."
    play sound sfx_open_dooor_campus_1
    "Открылась дверь."
    show mz normal pioneer far at center with dissolve
    "Я обернулась и увидела Женю."
    mz "О, ты уже проснулась. А то пришлось бы снова тебя будить."
    "Я улыбнулась ей в ответ и принялась собираться на пробежку."
    window hide
    stop ambience fadeout 2
    scene black with dissolve
    pause (2)
    play ambience ambience_forest_day fadein 3
    scene bg ext_path2_day:
        zoom 1.1
        yalign 0.01
        block:
            linear 0.2 pos (0,5)
            linear 0.2 pos (0,0)
        repeat
    with dissolve
    window show
    "Сегодня я решила пробежаться сквозь лес."
    "В отличие от более-менее открытой местности лагеря, где всё уже было прогрето солнцем, в лесу держалась ночная прохлада, а на траве блестели капельки росы, то и дело попадая мне на ноги."
    th "Так и заболеть недолго. Мне нужно уходить отсюда."
    "Но одну дорожку я уже пробежала мимо, а следующая всё никак не хотела попадаться."
    th "Да что ж за напасть-то!.."
    $ volume(0.2, "music")
    play music music_list["memories_piano_outdoors"] fadein 1
    scene bg ext_path_day:
        zoom 1.1
        yalign 0.01
        block:
            linear 0.2 pos (0,5)
            linear 0.2 pos (0,0)
        repeat
    with dissolve
    "К счастью, через пару минут я услышала где-то поблизости звуки музыки."
    th "Должно быть, это Мику! А значит, музклуб где-то поблизости."
    "И правда, сквозь деревья пробивался силуэт обители музыкантов (или, как в нашем случае, музыкантши). Мысленно поблагодарив её, я побежала к домику, а оттуда к умывальникам."
    window hide
    stop music fadeout 2
    stop ambience fadeout 2
    scene black with dissolve
    pause (2)
    $ volume(1.0, "music")
    play ambience ambience_camp_center_day fadein 2
    scene bg ext_house_of_sl_day with dissolve
    window show
    "Через пять минут с умыванием было покончено и, занеся вещи в домик, я пошла на линейку."
    
#линейка, завтрак
    scene bg ext_square_day 
    show mt normal panama pioneer far at center
    with dissolve
    "К моменту начала линейки на площади собралась от силы половина пионеров. Но вожатую это не очень-то и волновало, она приступила к объяснению планов на сегодня."
    "Предстояла большая уборка, а после неё – дискотека."
    show mt normal panama pioneer at center with dspr
    "Закончив с распорядком дня, Оля стала распределять обязанности. Все мои дела на сегодня были связанны с площадью: перед обедом нужно было её подмести, а после ужина украсить к дискотеке. Более того, мне ещё нужно было организовать других пионеров, чтобы они делали то же самое (но с дискотекой Оля обещала помочь)."
    "Выслушав все ценные указания, я отправилась на завтрак."
    stop ambience fadeout 2
    play ambience ambience_dining_hall_empty fadein 2
    scene bg int_dining_hall_day with dissolve
    "В столовой находилось всего лишь несколько пионеров (которых, кстати, не было на площади), большинство же ещё стояли и ждали распределения. Я выбрала один из множества свободных столиков и села за него. Сегодня давали кашу, хлеб, сосиску, яйца и компот."
    th "Впрочем, ничего удивительного."
    window hide
    scene black with dissolve
    pause (1)
    stop ambience fadeout 1
    play ambience ambience_dining_hall_full fadein 1
    scene bg int_dining_hall_people_day with dissolve
    window show
    "Покончив с завтраком, я уже собиралась выходить из столовой, но прямо перед дверью меня поймала Ольга Дмитриевна."
    show mt normal pioneer at center with dissolve
    mt "Славя, как хорошо, что я тебя нашла! Я же совсем забыла! Сходи в медпункт и забери там спирт для кибернетиков, только пусть расходуют его по назначению."
    sl "Хорошо."
    hide mt with dissolve
    th "Всё-таки она считает, что ребята могут его выпить.{w} Такое впечатление, что в этом вопросе Оля мыслит стереотипами."
    window hide
    stop ambience fadeout 2
    pause (2)
    
#в поисках водки
    play ambience ambience_medstation_inside_day fadein 2
    scene bg int_aidpost_day
    show cs normal glasses far at center
    with dissolve
    window show
    "В медпункте не было никого, кроме Виолы."
    sl "Здравствуйте, меня Ольга Дмитриевна послала за... спиртом."
    cs "Спиртом, говоришь? Да-да, припоминаю..."
    "Она подошла к шкафчику и, немного покопошившись, извлекла оттуда...{w} бутылку водки?"
    sl "Но... Разве это то, что нужно?"
    cs "А разве это не одно и то же? Вот, смотри, \"40\% спирта\" на этикетке написано. Для техники в самый раз. Только верните потом."
    "Похоже, что выбора у меня не было.{w} Я взяла бутылку и, поблагодарив медсестру, вышла на улицу."
    stop ambience fadeout 2
    play ambience ambience_camp_center_day fadein 2
    scene bg ext_aidpost_day with dissolve
    "Теперь передо мной вставал вопрос: а как же мне, собственно, дойти до клубов? Идти по всему лагерю сверкая бутылкой спиртного совершенно не хотелось."
    #"Иди через лес"
    th "Хм... А что если пройти через лес?{w} Да, замечательная идея, вот где точно никого не будет!"
    window hide
    stop ambience fadeout 2
    scene black with dissolve
    pause (2)
    play ambience ambience_camp_center_day fadein 2
    scene bg ext_clubs_day with dissolve
    window show
    "Через пять минут ходьбы (во время которой, к счастью, не было встречено ни одной живой души) я вышла к зданию клубов."
    stop ambience fadeout 2
    play sound sfx_open_door_clubs
    play ambience ambience_clubs_inside_day fadein 2
    scene bg int_clubs_male_day
    show el normal pioneer at cleft
    show sh normal pioneer at cright
    with dissolve
    "Внутри сидели Электроник и Шурик и, судя по запаху, что-то паяли."
    el "О, Славя! Добро пожаловать!"
    "Оторвался от своих забот Электроник."
    sl "Привет, я вот..."
    el "Спирт можешь поставить вот сюда, на стол."
    "Я последовала его совету."
    sh "Водка?"
    "Обернулся Шурик."
    sh "Ну да ладно, и она подойдёт."
    sl "Только не..."
    stop ambience fadeout 2
    show dv grin pioneer2 at center with dissolve
    "Я хотела передать им послание вожатой, но дверь отворилась и в помещение вошла Алиса.{w} Её взгляд упал на бутылку, явно выделяющуюся среди различного хлама, разбросанного по столу."
    play music music_list["always_ready"] fadein 2 #?
    dv "Ага, я смотрю, вы тут время даром не теряете."
    "Я невольно покраснела."
    show el surprise pioneer at cleft
    show sh serious pioneer at cright
    with dspr
    th "Эх, Виола..."
    show el serious pioneer at cleft with dspr
    el "Да это не то, что ты подумала!"
    el "Это медсестра дала нам на протирку контактов, а Славя только принесла!"
    dv "Ну конечно, конечно."
    dv "А стаканы где? Или вы без них, так удобнее?"
    show el sad pioneer at cleft with dspr
    "Алиса выглядела подозрительно спокойно для такой ситуации. А вот я больше не могла выдерживать насмешек и направилась к выходу."
    sl "Я пойду лучше."
    "Но у самой двери я предупредила кибернетиков (что могла бы сделать и раньше, если бы не Алиса):"
    sl "А вы, ребята, аккуратно. Медсестра просила вернуть, не расходуйте всё разом."
    dv "Да, ребята, разом лучше не надо! Она ж вас потом лечить откажется!"
    "Добавила Алиса и рассмеялась."
    "Я, продолжая краснеть всё больше, поспешила выйти за дверь."
    stop music fadeout 3
    play sound sfx_close_door_campus_1
    play ambience ambience_camp_center_day fadein 2
    scene bg ext_clubs_day with dissolve
    th "И как её только в пионеры приняли с такими замашками?.."
    "Первое дело уже было сделано и теперь мне нужно было пойти на площадь, дабы проконтролировать там процесс уборки."
    "Но стоять в стороне я не желала, решив, что буду подметать вместе со всеми."
    window hide
    scene black with dissolve
    pause (2)
    
#уборка
    play ambience ambience_camp_center_day fadein 3
    scene bg ext_square_day with dissolve
    window show
    "На площади стояло несколько пионеров. Увидев меня, они образовали подобие организованной толпы."
    sl "Ещё немного подождём и приступим к работе."
    "Пионеры вздохнули с облегчением: убираться они явно не хотели и даже несколько минут лишнего отдыха им были по душе."
    window hide
    pause (1)
    window show
    "..."
    window hide
    pause (2)
    window show
    "Примерно через десять минут подошло ещё несколько человек. Отослав нескольких из них за мётлами, я принялась распределять участки."
    show pi normal pioneer at center with dissolve
    "Ко мне подошёл Семён:"
    me "Привет!"
    sl "Ой, привет! Ты помочь пришёл?"
    me "Ну, не по своей воле, конечно…"
    "Он замолчал на полуслове."
    th "И он тоже не хочет работать. Такое впечатление, что никто в этом лагере не хочет работать."
    "Краем глаза я заметила, что мётлы уже были принесены со склада."
    sl "Понятно."
    sl "Бери метлу, твой участок – рядом с памятником."
    hide pi with dissolve
    "Семён оказался последним нераспределённым и я, взяв метлу, отправилась на другой конец площади."
    window hide
    play sound_loop sfx_broom_sweep fadein 2
    pause (1)
    window show
    "В целом, площадь не была особенно грязной, но подмести её было нужно. Всё осложняло палящее солнце, ведь на небе не было ни единого облачка. Ужасно хотелось присесть на близлежащую скамейку и отдохнуть, но я всё-таки домела свой участок и только после этого отправилась на отдых."
    "Видимо, закончил работать и Семён, который присел рядом со мной:"
    stop sound_loop
    play music music_list["smooth_machine"] fadein 5
    show pi normal pioneer at center with dissolve
    me "Хороший сегодня денёк…"
    sl "Да, только жарко."
    "Я посмотрела на небо. Облака даже и не думали появляться."
    me "Ты прямо ударница коммунистического труда!"
    sl "Да брось ты! Я просто люблю помогать другим."
    "Комплимент Семёна немного смутил меня."
    me "Это хорошо."
    sl "А ты?"
    me "Что я?"
    sl "Ну, я так понимаю, что тебя несколько тяготит общественная работа?"
    me "Да, наверное."
    sl "Почему?"
    me "Не знаю…"
    "Семён задумался."
    sl "Может, ты не любишь компанию?"
    me "Вполне возможно."
    th "Хотя бы он осознаёт свои проблемы. Ну, или пытается."
    sl "Чем займёшься, когда смена закончится?"
    me "В смысле?"
    sl "Ну, в институт будешь готовиться? Или какую-то специальность хочешь освоить?"
    me "Не знаю… А ты?"
    sl "У нас дома небольшое хозяйство, хочу родителям помогать."
    "Семён удивился, однако, ничего не сказал."
    sl "А твои родители кем работают?"
    me "Папа – в горкоме, мама – учителем."
    sl "Здорово."
    th "А вот родители у него – представители уважаемых профессий. И как они только смогли воспитать такого лентяя?"
    me "Наверное…"
    "Диалог зашёл в тупик. Семён принялся осматривать окрестности, однако старался не глядеть в мою сторону."
    th "Кажется, я смутила Семёна своими расспросами... Нужно его как-то подбодрить."
    sl "Знаешь, мне кажется, у тебя в жизни всё тоже будет хорошо!"
    show pi surprise pioneer at center with dspr
    me "С-спасибо…"
    "Пролепетал Семён."
    me "Но что ты имеешь в виду?"
    sl "Мне кажется, ты иногда очень пессимистично смотришь на жизнь."
    me "Возможно."
    sl "Но у тебя всё наладится!"
    me "Возможно."
    me "Ладно, надо дальше убираться."
    sl "Ага."
    hide pi with dissolve
    play sound_loop sfx_broom_sweep fadein 2
    "Я решила больше не смущать Семёна и пошла подметать дальше, хотя на участке уже было чисто."
    th "Нелёгкая это работа – объяснять человеку, что ему нужно делать {i}в глобальном плане{/i}."
    window hide
    pause (2)
    stop sound_loop
    stop music fadeout 3
    play sound music_list["dinner_horn_processed"]
    window show
    "Вскоре по лагерю заиграла музыка, призывающая на обед, и все, кто был задействован в уборке, отправились в столовую."
    th "Надеюсь, что они хотя бы всё домели."
    stop ambience fadeout 2
    
#обед
    play ambience ambience_dining_hall_full fadein 2
    scene bg int_dining_hall_people_day 
    show mt normal pioneer at center
    with dissolve
    "Столовая была битком. У входа стояла Ольга Дмитриевна, и она, видимо, спрашивала результаты уборки у каждого проходящего мимо.{w} Я тоже не избежала такой участи:"
    mt "Ну что, как прошла уборка?"
    sl "Замечательно! Всё подмели!"
    show mt smile pioneer at center with dspr
    mt "Хорошо. Будь так добра, загляни после обеда ещё и в библиотеку. Там не хватает людей."
    hide mt with dissolve
    "Зная характер Жени, для меня не было ни чего удивительного в том, что мало кто хочет работать в библиотеке. Согласившись кивком головы, я приступила к поиску свободных мест."
    show us laugh sport at center 
    show un normal pioneer at left 
    with dissolve
    "Хорошо, что таковые обнаружились рядом с Ульяной и Леной."
    "Я взяла обед и, спросив разрешения, присела рядом с ними."
    show pi normal pioneer at right with dissolve
    "Через минуту подошёл Семён:"
    me "Не возражаете, если я присяду?"
    sl "Да, конечно!"
    us "Будь любезен."
    "Слегка издевательски произнесла Ульяна."
    "Лена промолчала."
    window hide
    pause (1)
    window show
    sl "Пойдёшь сегодня на танцы?"
    "Спросила я первое, что пришло в голову, дабы развеять тишину, повисшую над столом."
    me "Не знаю."
    show us laugh2 sport at center with dspr
    us "Пойдёт, пойдёт! Куда он денется!"
    "Радостно вставила Ульяна."
    me "Ты-то точно пойдёшь…"
    show us surp1 sport at center with dspr
    us "Конечно! Не упущу случая посмотреть, как ты опростоволосишься."
    me "А ты?"
    "Семён перевел разговор из неприятной для него темы вопросом, обращённым к Лене."
    show us laugh sport at center with dspr
    show un smile pioneer at left with dspr
    un "Да…"
    "Ответила она коротко."
    sl "Видишь, значит, и тебе тоже стоит пойти."
    show us grin sport at center with dspr
    us "Не забудь фрак надеть."
    "Ульяна, видимо, была так довольна своей шуткой, что громко рассмеялась."
    me "Ты-то в чём придёшь, массовик-затейник?"
    show us laugh2 sport at center with dspr
    us "Се-к-рет!"
    show pi grin pioneer at right with dspr
    me "Платьице как на детском утреннике небось?"
    show us angry sport at center with dspr
    "Ульяна покраснела от злости – похоже, Семён смог её задеть."
    us "Нет, костюм химзащиты! Специально, чтобы от тебя не заразиться!"
    me "И чем ты планируешь от меня заразиться, интересно?"
    "Разговор принимал угрожающий оборот."
    show un normal pioneer at left with dspr
    sl "Ребята, хватит! Не ссорьтесь!"
    "Я попыталась остановить всё это, но, видимо, было уже поздно."
    show us surp1 sport at center with dspr
    us "Слабоумием, конечно же!"
    "Похоже, Ульянка опять была очень довольна своим ответом."
    me "Знаешь, если ты уже болеешь гриппом, то простудиться не получится."
    show us dontlike sport at center with dspr
    us "Это ты на что намекаешь?"
    me "Да так, ни на что…"
    "Он хитро отвёл взгляд."
    show us angry sport at center with dspr
    us "То есть ты хочешь сказать?.."
    "Ульяна снова покраснела."
    me "Я ничего не хочу сказать."
    show un surprise pioneer at left with dspr
    un "Ребята…"
    "Вмешалась Лена. Ситуация была накалена до предела."
    us "Ты допросишься!"
    me "Чего? Что ты поумнеешь наконец?"
    window hide
    play sound sfx_borshtch
    pause(1)
    show us laugh2 sport at center with dspr
    window show
    "Вместо ответа Ульянка взяла свою тарелку борща и опрокинула Семёну на голову."
    th "Чего и следовало ожидать!"
    show pi surprise pioneer at right with dissolve
    me "Ах ты, маленькая…"
    play music music_list["awakening_power"] fadein 2
    hide us with dspr
    "Она вскочила из-за стола и попыталась убежать, но Семён схватил её за руку."
    scene cg d3_us_dinner with dissolve
    "Немая сцена продолжалась несколько секунд."
    play sound sfx_throw_compote
    "Вдруг Ульяна ловко схватила компот и плеснула им в лицо и без того мокрому Семёну. Тот на мгновение потерялся и отпустил руку Ульяны."
    scene bg int_dining_hall_people_day with dissolve
    "Она побежала вглубь столовой, Семён – за ней..."
    window hide
    pause (3)
    window show
    "До момента остановки были опрокинуты несколько столов, разбита гора посуды и покалечено примерно пять пионеров."
    show us shy sport far at left
    show pi upset pioneer far at right
    with dissolve
    "И только теперь они, судя по всему, таки приступили к мирным переговорам. Из-за того, что они выдохлись, не было слышно ни слова."
    stop music fadeout 3
    show mt angry pioneer far at center with dissolve
    "Сзади к ним подошла Ольга Дмитриевна."
    stop ambience fadeout 1
    "В столовой воцарилась полнейшая тишина."
    th "Действительно, такой погром не мог пройти без последствий."
    mt "Ну что, довольны собой?"
    "Её голос звучал спокойно, но по выражению лица было понятно, что им не светит ничего хорошего."
    show mt rage pioneer far at center with dspr
    mt "И кто всё это должен убирать теперь?!"
    "Они молчали."
    mt "Кто, я спрашиваю, кто?!"
    "Ольга Дмитриевна потеряла контроль над собой и перешла на крик."
    show us surp3 sport far at left with dspr
    us "Он!"
    "Совершенно невозмутимо ответила Ульяна."
    me "Она."
    "Менее уверенно возразил Семён."
    mt "Оба!"
    "Поставила жирную точку в не успевшем начаться споре вожатая."
    "Однако Ульяна, кажется, вовсе не считала себя виноватой."
    show us surp2 sport far at left with dspr
    us "Вот ещё!{w} Не буду я ничего убирать!"
    us "Это он всё! Он начал!"
    me "Вот и нет!"
    show us dontlike sport far at left with dspr
    us "Вот и да!"
    show mt angry pioneer far at center with dspr
    mt "Я не собираюсь разбираться в этих глупостях!"
    play ambience ambience_dining_hall_full fadein 5
    mt "Семён, иди возьми швабру, совочек, тряпку и так далее в кладовке, а ты!!!"
    hide pi
    show mt rage pioneer far at center 
    with dspr
    "Она перевела взгляд на вторую участницу конфликта."
    show us fear sport far at left with dspr
    mt "Ты!!!{w} Немедленно начинай собирать разбитую посуду!"
    "Оля явно считала Ульяну более виноватой, нежели Семёна."
    th "Оба хороши."
    "Ольга Дмитриевна отдышалась немного и продолжила:"
    show mt angry pioneer far at center with dspr
    mt "От тебя одни проблемы! Сколько раз я тебе говорила, что нельзя носиться по всему лагерю как угорелой? Что нельзя доводить ругань и споры до вот таких вот финалов? Что..."
    
#у вас Семён убежал
    hide mt
    hide us
    with dspr
    show pi normal pioneer far at left with dspr:
        linear 3.0 xalign -1.0
    "Вожатая продолжала свою тираду, но теперь моё внимание привлекало совершенно другое: Семён подошёл к кладовке, находящейся рядом со входом, постоял рядом и... вышел на улицу."
    th "Куда это он? Ему же потом ещё сильнее влетит!"
    hide pi with dspr
    window hide
    menu:
        "Пойти за Семёном":
            $ sl_m_lp += 2
            $ sl_m_day3_help = True
            window show
            th "Нужно ему помочь."
            "Я встала из-за стола и, в обход Ольги Дмитриевны и Ульяны, дошла до выхода."
        "Остаться на месте":
            window show
            th "Хотя... Думаю, он пошёл на это осознанно и мои попытки помочь только навредят."
            "Ольга Дмитриевна закончила ругать Ульяну и теперь направлялась к нашему столу."
            show mt normal pioneer at center with dissolve
            mt "Славя, сходи за Семёном, он куда-то убежал. Сходи и приведи ко мне."
            sl "Уже иду."
            th "Видимо, мне в любом случае пришлось бы идти за Семёном."
            hide mt with dissolve
            "Я встала из-за стола и прошла к выходу."
    stop ambience fadeout 2
    play ambience ambience_camp_center_day fadein 2
    scene bg ext_dining_hall_near_day with dissolve
    th "Итак, куда же он мог пойти?..{w} Наверное, стоит поискать в лесу. Его там часто можно найти.{w} Леший, не иначе."
    window hide
    stop ambience fadeout 2
    scene black with dissolve
    pause (2)
    
#лес
    play ambience ambience_forest_day fadein 3
    scene bg ext_polyana_day
    show pi normal pioneer at center
    with dissolve
    window show
    "Семёна я нашла на пеньке около одной из множества тропинок, раскиданных по всему лесу."
    play music music_list["take_me_beautifully"] fadein 5
    sl "Как погодка?"
    "Семён поднял голову и посмотрел на меня."
    me "Ничего…"
    sl "А я так и думала, что найду тебя здесь."
    me "Почему?"
    sl "Не знаю, просто…"
    th "И зачем я вообще это сказала?.."
    hide pi with dspr
    "Я посмотрела на небо. Солнце потихоньку, едва заметно начинало своё движение к закату. Облака так и не появлялись."
    show pi normal pioneer at center with dspr
    sl "Здесь меньше людей."
    me "А ты… что здесь делаешь?"
    sl "Тебя ищу."
    show pi surprise pioneer at center with dspr
    me "Меня? Зачем?"
    if sl_m_day3_help:
        th "А действительно, зачем?.."
        th "Точно, он поможет мне в библиотеке!"
    else:
        "И тут у меня в голове созрел план: я не стану выдавать Семёна вожатой."
        th "Лучше он мне в библиотеке поможет, а после придумаю что-нибудь."
    sl "Не догадываешься?"
    sl "Понимаю, что ты виноват меньше, чем Ульяна."
    sl "Но раз уж не стал помогать ей, то поможешь мне."
    hide pi with dspr
    "Я вновь посмотрела на небо. Что не удивительно, оно ничуть не изменилось."
    show pi normal pioneer at center with dspr
    me "И в чём помощь заключается?"
    sl "Нужно разобрать книги в библиотеке."
    me "Весь в вашем распоряжении, мадемуазель. Или мадам?"
    "Это прозвучало настолько забавно, что я рассмеялась."
    stop ambience fadeout 2
    sl "Тогда пройдёмте со мной, месье!"
    window hide

#билиотека
    scene bg ext_library_day
    show pi normal pioneer at center
    with dissolve
    window show
    "Через несколько минут мы подошли к библиотеке."
    "Семён всю дорогу искал слежку за нами, но никого так и не обнаружил. Да и мало кто мог пойти к библиотеке, зная, {i}кто{/i} там обитает."
    stop music fadeout 3
    scene bg int_library_day
    show pi normal pioneer at center
    with dissolve
    play ambience ambience_library_day fadein 3
    "Внутри библиотеку освещал яркий солнечный свет, в котором купались мириады пылинок."
    "Это было весьма завораживающе, но нам нужно было работать."
    sl "Давай ты начнёшь с тех рядов."
    "Я показала на книжный шкаф около бюста Владимира Ильича Ленина."
    me "Хорошо, а что конкретно делать надо?"
    sl "Просто складывай все книги на пол.{w} Мы их протрём, потом назад поставим."
    me "А где Женя?"
    th "Действительно, где?"
    sl "Не знаю, ушла куда-то."
    hide pi with dissolve
    "Мы принялись за работу."
    window hide
    pause (2)
    window show
    sl "Ну как?"
    "Спросила я у Семёна, находившегося с другой стороны шкафа."
    me "Нормально, работаем потихонечку."
    me "А ты читала что-нибудь из этого?"
    sl "Из чего?"
    me "Ну, про коммунизм…"
    sl "Нет, мне больше историческая литература нравится.{w} Приключения ещё."
    me "И мне…"
    "В голову пришла мысль, что неплохо бы примирить Семёна и Ульяну. И начинать нужно было с него:"
    sl "Ты не обижайся на Ульяну."
    me "Не обижаюсь..."
    sl "Она не со зла всё это."
    "«А с обыкновенной бешености» - хотела добавить я, но удержалась. Всё-таки, у нас сейчас более-менее серьёзный разговор."
    me "Со зла или нет, но надо думать, что делаешь!{w} Впрочем, ладно..."
    show pi normal pioneer at center with dissolve
    "Работа продвигалась успешно. Я сняла уже все книги с полок и решила проверить, как идут дела у Семёна:"
    sl "Закончил?"
    me "Да, кажется."
    sl "И я.{w} Ладно, давай я буду протирать, а ты – складывать назад."
    me "Так я не помню, в каком они порядке стояли."
    sl "Ничего, всё равно их никто не читает."
    "Я слегка улыбнулась, потупив глаза: помощница вожатой не должна такого говорить!"
    me "Да, наверное.{w} По крайней мере, я бы такое читать точно не стал."
    "Я протирала книги, Семён ставил их на полки."
    "Работа продвигалась быстро, так что мы уже почти всё закончили.{w} Но вдруг я заметила, что не было снято ни одной книги с самого верха шкафа."
    sl "А с верхних полок чего не снял?"
    me "Верхних?"
    "Он посмотрел наверх, удостоверяясь в наличии таковых."
    me "Прости, не заметил."
    sl "Ничего."
    me "Сейчас!{w} Стул только принесу."
    hide pi with dissolve
    "Он встал на стул и мы продолжили работать как прежде: он снимает, я протираю, он ставит."
    "Через некоторое время остался всего десяток-другой в дальнем конце полки.{w} Семён решил достать их не меняя местоположения, как вдруг..."
    window hide
    
#падение
    play sound sfx_chair_fall
    with vpunch
    play sound2 sfx_bodyfall_1
    $ renpy.pause(1)
    scene cg d3_sl_library_an with fade
    stop ambience fadeout 2
    play music music_list["eternal_longing"] fadein 5
    window show
    "На меня упало что-то тяжёлое.{w} Открыв глаза, я поняла, что это был Семён."
    me "Ты не… Я тебя не… Жива?!"
    "Он был сильно испуган."
    sl "Жива."
    "Мы находились странно близко друг к другу...{w} Но Семён решил сначала справиться о моём здоровье и самочувствии."
    me "Ничего не сломала?"
    sl "Вроде нет."
    scene cg d3_sl_library_2_an with dspr
    sl "Из тебя плохой каскадер выйдет."
    "Сказала я и рассмеялась."
    me "Точно."
    "Семён смотрел мне в глаза.{w} Просто смотрел, даже не шевелясь. Во мне проснулось любопытство, что же он дальше делать-то будет?"
    "Его лицо нависало над моим, буквально в паре миллиметров. Казалось ещё чуть-чуть и..."
    "Но ничего не происходило.{w} Мы лежали и смотрели друг другу в глаза."
    "И тут с улицы послышалась музыка."
    $ volume(0.6, "music")
    $ volume(0.7, "sound")
    play sound music_list["dinner_horn_processed"]
    sl "Ужин."
    me "Угу…"
    sl "Пойдём?{w} Или ещё полежим?"
    "Я улыбнулась.{w} Возможно, сейчас..."
    me "Пойдём…"
    "Сказал он, но продолжил лежать."
    sl "Чтобы пойти, нужно сначала встать."
    me "Очевидно…"
    "Семёна словно пригвоздило к полу. Я решила, что дальше так лежать нельзя и аккуратно выползла из-под него."
    stop music fadeout 3
    play ambience ambience_library_day fadein 3
    scene bg int_library_day with dissolve
    th "Значит, не судьба..."
    "Кажется, меня всё это слегка расстроило. Но это нельзя было показывать."
    th "Это... Это всё неправильно!"
    stop sound fadeout 2
    "Он не менял положения."
    sl "Так и будешь лежать?"
    "Полулежащий в нелепой позе Семён внезапно показался мне невероятно смешным и я вновь рассмеялась."
    "Тут его словно что-то в голову ударило и он, наконец-то, встал."
    show pi surprise pioneer at center with dissolve
    me "Прости…"
    sl "Да ничего."
    sl "Только мы не закончили.{w} Надо будет потом остальное убрать."
    show pi normal pioneer at center with dissolve
    me "Да, конечно."
    window hide
    stop ambience fadeout 2

#улица
    scene bg ext_dining_hall_away_day with dissolve
    play ambience ambience_camp_center_day fadein 3
    window show
    "Всю дорогу до столовой Семён молчал. Я рассказывала ему о предстоящей подготовке к дискотеке, о том, что ещё будет в смене, но он ничего не слушал, погрузившись в свои мысли."
    scene bg ext_dining_hall_near_day with dissolve
    $ volume(1.0, "sound")
    $ volume(1.0, "music")
    play music music_list["revenga"] fadein 2
    show mt rage pioneer at cright
    show pi normal pioneer at cleft
    with dissolve
    "На крыльце столовой стояла Ольга Дмитриевна."
    mt "Ничего не хотите мне сказать, молодой человек?"
    show pi surprise pioneer at cleft with dspr
    th "Ой, а вот про это-то я и забыла... Ничего, выкрутимся как-нибудь."
    me "Нет…"
    mt "Как уборка прошла?"
    me "Хорошо…"
    mt "А мне Ульяна сказала, что не очень."
    me "Так это она виновата!"
    mt "Я что тебе сказала?"
    mt "Чтобы вы вместе убрались!{w} А ты?!"
    sl "Не ругайте его!{w} Семён мне помог в библиотеке."
    mt "Правда?"
    sl "Правда!"
    th "Фух, вроде бы, пронесло."
    show pi normal pioneer at cleft with dspr
    mt "Это, конечно, замечательно, но…{w} В общем, останешься без ужина!"
    show pi surprise pioneer at cleft with dspr
    me "Как, почему?!"
    "Попытался протестовать Семён."
    mt "В следующий раз будешь знать, что бывает с теми, кто меня не слушается!"
    sl "Простите его!"
    "Слова вырывались сами собой"
    sl "Под мою ответственность!"
    "Оля задумалась."
    show mt normal pioneer at cright with dspr
    mt "Ладно уж!"
    sl "Спасибо!"
    stop music fadeout 3
    th "Получилось!"
    me "Спасибо…"
    "Семён, если и был рад, то старался этого не показывать."
    
#ужин
    scene bg int_dining_hall_people_day
    show pi normal pioneer at center
    with dissolve
    play ambience ambience_dining_hall_full fadein 3
    "Мы быстро вошли в столовую."
    sl "Ладно, я к девочкам сяду, мы с ними договорились.{w} Не скучай!"
    "Я улыбнулась и помахала ему рукой."
    "Увы, но сидящих за этим столом я не знала. Мне просто нужно было отвязаться от Семёна, слишком уж много сегодня произошло..."
    "А вот Ольга Дмитриевна поступила бесчеловечно. Семён, конечно же, был виноват, но лишать человека пищи из-за этого... Это какое-то средневековье!{w} Да и к тому же она даже не попыталась разобраться, Ульяна была виновата больше чем Семён."
    th "Впрочем, сама хороша. Кто начал разговор-то?"
    window hide
    pause (3)
    window show
    "С ужином было покончено и я поспешила выйти из столовой. Предстояла подготовка дискотеки, за которую я также была ответственна."
    "Первым делом я пойду в клубы – за аппаратурой."
    window hide
    stop ambience fadeout 2
    scene black with dissolve
    pause (2)
    
#подготовка дискотеки
    $ sunset_time()
    $ persistent.sprite_time = "sunset"
    play ambience ambience_clubs_inside_day fadein 2
    scene bg int_clubs_male_sunset
    show sh normal pioneer at center
    with dissolve
    window show
    "В клубе сидел и что-то мастерил Шурик."
    sl "Ты же ведь помнишь, что нужно было приготовить аппаратуру к дискотеке?"
    sh "Да, конечно. Сейчас придёт Электроник и потащим."
    hide sh with dissolve
    "Он ещё не успел договорить последнее слово, когда в комнату вошёл кудрявый кибернетик."
    "Кивнув Шурику, он пошёл в тот угол помещения, где лежало оборудование. Глава кружка пошёл следом за ним."
    window hide
    stop ambience fadeout 2
    scene black with dissolve
    pause (2)
    play ambience ambience_camp_center_evening fadein 2
    scene bg ext_square_sunset with dissolve
    window show
    "Вскоре наша процессия из двух кибернетиков, колонки и меня с проводами подошла к площади. Там нас ждала Ольга Дмитриевна и ещё несколько пионеров, а также столы и стулья, которые они принесли. Всех представителей сильного пола отправили обратно в клубы, а я сбросила провода на землю и принялась ждать."
    #музыка?
    play music music_list["everyday_theme"] fadein 3
    show mt smile pioneer at center with dissolve
    mt "Ну что, Славя, как день?"
    sl "Нормально, а после дискотеки станет ещё лучше!"
    th "Высказывать вожатой всё, что я о ней за сегодня подумала, совершенно не хочется... Не то место и не то время, как в таких случаях говорят."
    if sl_m_day1_help_od:
        show mt sad pioneer at center with dspr
        mt "Я вот тут спросить всё хотела... Почему ты всё время с Семёном рядом оказываешься?"
        "Я сильно удивилась таким вопросам."
        sl "А что плохого в том, чтобы помогать людям?"
        show mt normal pioneer at center with dspr
        mt "Да нет, ничего. Просто, если что, можешь говорить мне."
        sl "Хорошо."
        th "Изливать душу вожатой? Да, мы с ней почти что на равных, да и говорим в целом-то по дружески, но разговоры по душам...{w} Не думаю."
        "От очередных расспросов меня спасло возвращение «посланцев за техникой». Они отправились обратно, но на этот раз Шурик остался подключать провода."
    else:
        show mt normal pioneer at center with dspr
        "Разговор на этом был окончен и мы принялись ждать «посланцев за техникой»."
        window hide
        pause (2)
        window show
        "Наконец, они пришли, оставили то, что принесли и отправились за следующей партией. Шурик остался на площади и стал подключать провода."
    show mt surprise pioneer at center with dspr
    mt "Ой, а про гирлянды-то я забыла!"
    "Внезапно опомнилась Ольга Дмитриевна."
    mt "Славя, будь добра, принеси их со склада."
    sl "Иду!"
    hide mt with dissolve
    if sl_m_day2_sp_keys:
        "Я вскочила и направилась в сторону сарая, гордо именуемого «складом»."
        window hide
        scene black with dissolve
        stop music fadeout 3
        stop ambience fadeout 2
        pause (2)
        play ambience ambience_camp_center_evening fadein 2
        scene bg ext_square_sunset_party with dissolve
        window show
        "Пока я ходила за гирляндами и лестницей (которой, совершенно случайно, тоже не оказалось на площади), мальчики принесли всё оборудование и даже подключили. Одного из них отправили развешивать те самые гирлянды, остальных же просто отпустили."
        show mt normal pioneer at center with dissolve
        sl "Что делать дальше?"
        "Спросила я у вожатой."
    else:
        "Я вскочила и направилась в сторону сарая, гордо именуемого «складом». Но тут меня словно что-то остановило."
        th "А как же я попаду туда без ключей? Я же ведь совсем про них забыла!.."
        show mt normal pioneer at center with dissolve
        sl "Ольга Дмитриевна, склад ведь закрыт?"
        show mt surprise pioneer at center with dspr
        mt "Да, а почему ты спрашиваешь?"
        "Вожатая смотрела на меня непонимающе."
        sl "Я... Я ключи так и не нашла..."
        "Было неимоверно стыдно в этом признаваться, ведь я помощница вожатой – и такая растеряша!"
        show mt normal pioneer at center with dspr
        mt "Так вот ты про что. Я тоже их не видела. На вот, возьми мои."
        "Она протянула мне связку ключей."
        mt "Шурик! Иди-ка сюда!"
        show mt normal pioneer at center:
            linear 1.0 xalign 0.355
        show sh normal pioneer at cright
        with dissolve
        "По всей видимости, он уже подключил всё, что можно и быстро явился на зов вожатой."
        mt "Шурик, иди со Славей и принеси лестницу со склада."
        "Он кивнул головой и мы отправились в путь."
        window hide
        scene black with dissolve
        stop music fadeout 3
        stop ambience fadeout 2
        pause (3)
        play ambience ambience_camp_center_evening fadein 2
        scene bg ext_square_sunset_party with dissolve
        window show
        "К тому моменту, как мы вернулись на площадь, оборудование уже стояло на своих местах. Поставив лестницу у дерева, Шурик отправился подключать и настраивать. Я же отдала гирлянды одному из «грузчиков», которого отправили развешивать те самые гирлянды, остальных же просто отпустили."
        show mt normal pioneer at center with dissolve
        sl "Что делать дальше?"
        "Спросила я у вожатой, отдавая ключи."
    mt "Да ничего, можешь тоже идти, готовиться сама."
    hide mt with dissolve
    "Я оглянулась. У пульта управления Шурик что-то настраивал, на деревьях потихоньку появлялись те самые гирлянды."
    th "Пожалуй, моя помощь здесь действительно больше не требуется."
    window hide
    stop ambience fadeout 2
    scene black with dissolve
    pause (1)
    
#домик перед дискотекой (1)
    play ambience ambience_int_cabin_evening fadein 2
    scene bg int_house_of_sl_sunset with dissolve
    window show
    "Я вернулась в домик. Внутри было темно и пусто. Я зажгла свет и посмотрела на часы. 19:43. До дискотеки было менее полутора часа."
    "Хотелось что-то сделать, но делать было нечего.{w} Я легла на кровать и уставилась в потолок. Но и на две минуты такого занятия меня не хватило. Спать тоже не хотелось."
    "Осмотрев висящее в шкафу платье (ничуть не помялось и не повредилось), мой взгляд упал на дневник. Я открыла его, взяла ручку и продолжила писать:"
    
#блокнот (1)
    show bknt_clear at truecenter with dspr
    play sound_loop pen_write
    show bknt_w8 at truecenter with dissolve2
    $ persistent.sl_m_bknt8 = True
    th "Десятый день в \"Совёнке\". День был трудным и насыщенным. Сначала я отнесла спирт (в виде водки – спасибо медсестре) кибернетикам, за что нас обсмеяла вездесущая Алиса. Потом руководила уборкой площади."
    th "После обеда я ловила сбегавшего от наказания Семёна. Зато он здорово помог мне в библиотеке. Вот только упал на меня в самом конце, но мы остались живы. Перед ужином Оля проявила неслыханное варварство: она хотела оставить Семёна без ужина. Но узнав, что он работал вместе со мной в библиотеке, отступилась (после того, как я поручилась за него)."
    th "А ещё я помогала готовить дискотеку."
    stop sound_loop
    hide bknt_clear
    hide bknt_w8
    with dspr
    
#домик перед дискотекой (2)
    "На этом я закончила, решив, что потом допишу ещё и про то, как она, дискотека, прошла."
    play sound sfx_open_dooor_campus_1
    show mz normal glasses pioneer at center with dissolve
    "Пока я убирала дневник, в комнату вошла Женя."
    sl "Женя, а ты идёшь на дискотеку?"
    show mz angry glasses pioneer at center with dspr
    mz "Вот ещё. Нужны мне больно эти ваши глупости."
    sl "А если заметят, что тебя не было?"
    show mz normal glasses pioneer at center with dspr
    mz "Ой, да как будто бы она следить будет. Сама, небось, в пляс бросится да и забудет про всё."
    "Я оглянулась на часы. 20:20. Пора собираться и выходить."
    sl "Ну а ты-то что делать будешь?"
    "Спросила я, доставая платье."
    mz "Читать, конечно же."
    hide mz with dissolve
    "Диалог можно было считать законченным. Я надела платье, причесалась и пошла на площадь."
    window hide
    stop ambience fadeout 2
    scene black with dissolve
    pause (2)
    
#площадь перед дискотекой
    $ night_time()
    $ persistent.sprite_time = "night"
    play ambience ambience_camp_center_evening fadein 2
    scene bg ext_square_night_party2 with dissolve
    window show
    "На площади было немноголюдно, а если точнее – только Шурик и Электроник, что неудивительно: сейчас было примерно полдевятого, а дискотека только в девять."
    show el smile pioneer at cleft
    show sh normal pioneer at cright
    with dissolve
    sl "Ну как, у вас всё работает?"
    el "Да, всё просто замечательно! Сейчас вожатая с музыкой придёт – и можно начинать!"
    window hide
    scene black with dissolve
    pause (1)
    scene bg ext_square_night_party2 with dissolve
    window show
    "Через пару минут на тропинку вышла Ольга Дмитриевна с кипой пластинок и несколькими кассетами."
    show mt normal dress at center
    show el smile pioneer at left
    show sh normal pioneer at right
    with dissolve
    el "Ну вот, можно и начинать!"
    mt "Подожди остальных."
    "Электроник присмотрелся и понял, что площадь ещё пуста."
    el "Ну ничего. Вот все придут и начнём."
    window hide
    scene black with dissolve
    pause (1)
    scene bg ext_square_night_party2 with dissolve
    window show
    "Ближе к девяти вечера на площадь начали стекаться пионеры. Я увидела Лену, Ульяну и даже Виолу."
    th "Остальных знакомых, видимо можно и не ждать."
    window hide
    scene black with dissolve
    pause (1)
    scene bg ext_square_night_party2 with dissolve
    window show
    "К девяти вечера на площади собралась добрая половина лагеря. Оля сказала, что ещё немного подождём и только потом начнём."
    "Внезапно, ко мне подбежала Ульяна:"
    show us surp1 dress at center with dissolve
    us "Смотри-смотри, там тот-кто-не-пойдёт-на-дискотеку!"
    hide us with dspr
    "Она показала пальцем в сторону сидящего на скамейке Семёна и побежала дальше."
    th "Пойду, поздороваюсь, что ли."
    show pi normal pioneer at center with dissolve
    sl "Привет!"
    me "Привет."
    "Я села рядом."
    sl "Как тебе вечер?"
    me "Нормально."
    sl "Что ты такой грустный?"
    me "Да ничего…"
    th "Видимо, он не очень-то хотел сюда идти."
    sl "Ладно, потанцуешь – развеселишься!"
    me "Наверное…"
    sl "Не забудь один танец и для меня оставить!"
    hide pi with dissolve
    "Я засмеялась и вернулась к аппаратуре."
    window hide
    scene black with dissolve
    pause (1)
    scene bg ext_square_night_party2 with dissolve
    window show
    "Похоже, уже весь лагерь собрался на площади. Пионеры стояли большими группами, о чём-то разговаривали, шутили и смеялись."
    "Ульяна пыталась уговорить поставить для начала что-нибудь энергичное, но Ольга Дмитриевна была непреклонна и собиралась включать что-то из современной эстрады."
    window hide
    
#дискотека
    scene bg ext_square_night_party with dissolve
    play music music_list["lightness"] fadein 2
    window show
    "И вот, момент настал. Пластинка закрутилась."
    "Все некоторое время просто стояли, словно не слыша музыки."
    th "Всегда трудно сделать первый шаг..."
    show us surp1 dress far at center with dspr
    "Но, по всей видимости, только не для Ульяны. Она выбежала на середину площади и закричала:"
    us "Чего стоим?!"
    "И начала танцевать. Своеобразно, конечно, но что поделаешь – как умеет."
    us "Эй, Семён!"
    "Выкрикнула Ульяна. Он смеялся и как будто бы не слышал её."
    us "Не притворяйся! Иди-ка сюда."
    "Не смотря на все её усилия, Семён продолжал делать вид, что Ульяны просто не существует."
    hide us  with dissolve
    "Пионеры потихоньку поняли, что от них требуется на этом празднике жизни, и пустились в пляс."
    "Я подошла к Семёну:"
    show pi normal pioneer at center with dissolve
    sl "Семён, чего сидишь?{w} Потанцевать не хочешь?"
    me "Да что-то не хочется…"
    sl "Точно?"
    me "Попозже, может быть."
    "Семён слегка задумался."
    me "Ладно, может, чуть-чуть…"
    sl "Вот и правильно!"
    "Я протянула ему руку."
    show pi shocked pioneer at center with dspr
    "Мы стояли в середине танцующей толпы."
    "Я начала пританцовывать, а Семён продолжал стоять, оглядываясь по сторонам. На его лице явно читалось выражение «что я здесь вообще делаю?»."
    stop music fadeout 3
    "Вдруг музыка стихла."
    "Некоторые продолжали танцевать по инерции, но большинство всё же остановилось."
    mt "Без паники!"
    "Оля подошла к диджейскому пульту и что-то там покрутила."
    th "Видимо, кибернетики тоже ушли танцевать."
    mt "Следующая песня – дамы приглашают кавалеров!"
    if sl_m_day1_help_od:
        "Я заметила, что она подмигнула кому-то в толпе.{w} Не мне ли часом?{w} Вспоминая наш разговор, может статься, что это действительно так."
    
#танец
    show pi normal pioneer at center with dspr
    "Семён посмотрел на меня. Я улыбнулась и протянула ему руку. Он не отказал."
    scene cg d3_sl_dance_an with dissolve
    stop ambience fadeout 2
    play music music_list["confession_oboe"] fadein 2
    "Несколько минут мы кружились в танце."
    "Я (совершенно неожиданно для самой себя) начала смущаться и краснеть, но всё равно смотрела Семёну в глаза."
    "Они мельтешили из стороны в сторону. Его движения были слегка скованными, а сам он изредка подрагивал, но явно не от ужаса, а скорее от другого: ему понравилось танцевать!"
    "На душе было удивительно спокойно. Я поняла, что могу так танцевать ещё очень долго, до полного изнеможения."
    "Внезапно Семён резко изменил выражение лица на какое-то полунедовольное-полуустращающее."
    sl "Что-то не так?"
    "Спросила я полушёпотом."
    me "Да нет, всё хорошо...{w} То есть отлично."
    sl "Ты так напряжён."
    me "Есть немного."
    sl "Мальчики не любят танцевать."
    me "Пожалуй..."
    "Я лишь улыбнулась ему в ответ.{w} Мне... Мне действительно нравилось танцевать с Семёном."
    window hide
    pause (1)
    window show
    "Песня закончилась."
    stop music fadeout 3
    "Семён по инерции продолжил обнимать меня, но я с лёгкостью выскользнула из его рук."
    scene bg ext_square_night_party
    show pi normal pioneer at center
    with dissolve
    play ambience ambience_camp_center_evening
    sl "Спасибо за танец!"
    me "Это тебе спасибо."
    "Произнёс Семён и просто застыл, смотря на меня."
    "Заиграла следующая мелодия."
    sl "Ещё?"
    me "Нет, пожалуй, передохну."
    hide pi with dissolve
    "Ответил Семён и пошёл... к выходу с площади?"
    th "Я... Я что-то сделала не так?.. Но вроде же всё было хорошо!"
    "Я пыталась танцевать и дальше, но поведение Семёна не желало выходить у меня из головы. Напротив, все силы моего разума были направлены на поиск каких-либо ошибок."
    "Так дальше продолжаться не могло: мне нужно было найти Семёна."
    th "Он пошёл в сторону столовой... Значит и мне нужно идти в том направлении!"
    window hide
    scene black with dissolve
    pause (2)
    
#поиски Семёна
    scene bg ext_boathouse_night with dissolve
    window show
    "Семёна не было на лодочной станции..."
    scene bg ext_dining_hall_away_night with dissolve
    "Не было около столовой "
    scene bg ext_shed_night with dissolve
    extend "и около склада..."
    stop ambience fadeout 2
    
#пляж
    play ambience ambience_lake_shore_night fadein 3
    scene bg ext_beach_night with dissolve
    "Но вот на пляже я заметила чью-то тень.{w} И точно, это был Семён, сидящий на песке и смотрящий на водную гладь."
    stop ambience fadeout 2
    scene cg d3_sl_evening with dissolve
    play music music_list["forest_maiden"] fadein 2
    sl "Вот ты где!"
    "Воскликнула я, смотря на него сверху."
    sl "Чего ты так быстро убежал?"
    me "Да просто захотелось побыть одному."
    "Он говорил спокойно, да так, словно его тут и не было, а ответы были записаны автоматическим голосом на плёнку."
    sl "Красиво тут, да?"
    "Речка действительно выглядела прекрасно: на неё падал лунный свет, создавая «дорожку», а спокойствие воды нарушал лишь редкий слабый ветерок, из-за чего создавалось впечатление, что «дорожка» движется, и если на неё встать, то можно уехать к другому берегу..."
    me "Красиво…"
    "Всё так же отчуждённо согласился Семён."
    sl "Может, ещё пойдём потанцуем?"
    me "Нет, мне и тут неплохо."
    "Семён словно сильно задумался и не собирался реагировать ни на что и ни на кого. Даже если вода перед ним сейчас начала бы гореть, а из неё выползать трёхглазые шестирукие инопланетяне, он бы не обратил на это ни капли внимания."
    sl "О чём думаешь?"
    me "Ни о чём…"
    sl "Нельзя ни о чём не думать!"
    "Я состроила обиженное лицо, но быстро сменила его на улыбку, ведь на самом-то деле я не обиделась."
    sl "Ты всегда о чём-то думаешь, даже если и не замечаешь этого."
    me "Наверное."
    sl "Так о чём сейчас?"
    me "О совах."
    sl "А почему о совах?"
    "Я рассмеялась. Такой ответ казался несколько глупым...{w} но вдруг и он что-то да значит?"
    me "Не знаю."
    sl "Ты когда-нибудь видел сов?"
    me "Конечно видел."
    sl "И они тебе нравятся?"
    me "Птица как птица."
    sl "Ночная птица."
    "Уточнила я с небольшим намёком."
    th "Прямо как он, не иначе – ложится и встаёт поздно, а ещё любит скрываться в тени."
    "В моей голове возник образ совы с головой Семёна – небольшое тельце и огромная по сравнению с ним голова, с длинной чёлкой и почти не видимыми глазами."
    me "Да, ночная птица."
    "Однако, он ничего не заподозрил или даже не стал вслушиваться в мои слова."
    sl "А ты похож на сову?"
    me "В смысле?"
    sl "Ну, знаешь, бывают же совы и жаворонки. Кто-то рано утром встаёт, а кто-то любит поспать."
    me "Знаю."
    sl "Так кто ты?"
    me "Сова, наверное…{w} Люблю поспать."
    th "Я догадалась!"
    sl "А вот я жаворонок.{w} Чем раньше встанешь, тем больше успеешь сделать за день!"
    me "Славя, а у тебя вообще никаких проблем в жизни не бывает?"
    sl "Ты это о чём?"
    "Во второй раз за этот вечер Семёну удалось удивить меня (первым был, естественно, его побег с площади)."
    me "Ну, ты всегда такая радостная, всегда готова помочь, с лёгкостью за всё берёшься."
    me "Кажется, что тебя ничего не может расстроить…"
    sl "Почему же!"
    "Я рассмеялась."
    sl "Я обычный человек."
    me "А вот мне иногда кажется, что я здесь совсем чужой."
    sl "Тебе не нравится этот лагерь??"
    me "Ну, я не совсем про лагерь.{w} И в прошлой…"
    "Он осёкся и исправился."
    me "И дома тоже.{w} Что я не такой, как все. Что мне здесь не место…"
    "Семён словно проснулся от своего странного сна и стал живо отвечать на мои вопросы."
    sl "Да брось ты! Глупости это всё."
    me "Нет, на самом деле.{w} При других обстоятельствах ты бы и не обратила на меня внимания.{w} Я ведь совсем не такой, как ты. Я ленивый, необщительный, особыми талантами не блещу."
    me "Из толпы в большом городе я бы был последним, кого ты бы заметила.{w} Впрочем, я вообще не так часто выхожу из дома."
    "Его речи стали больше похожи на бред безумца."
    sl "Семён, ты меня пугаешь."
    "Сказала я и серьёзно посмотрела на него."
    me "А что, я разве не прав?"
    sl "Конечно нет!{w} Ведь ты – это ты. Другого такого нет. Тебе надо просто чуточку уверенности в себе и терпения, и всё придёт!{w} Обязательно."
    "Семён поднял голову в небо, словно собирался искать поддержки в этом, судя по всему трудном для него разговоре, из космоса."
    me "Если бы всё было так просто…"
    sl "А ничего сложного и нет! Давай начнём прямо сейчас!"
    me "Что начнём?"
    sl "Меняться!"
    me "И как ты себе это представляешь?"
    sl "Надо сделать что-нибудь полезное!"
    me "Например?"
    sl "Ну…"
    "Было очень сложно придумать какое-либо полезное занятие поздно вечером."
    sl "Пойдём уберёмся на площади.{w} Дискотека-то уже закончилась!"
    me "Вдвоём?"
    sl "Да там немного.{w} Главное – сделать первый шаг!"
    me "Может, завтра?{w} Вместе со всеми?"
    sl "Зачем до завтра откладывать?{w} Разве тебе не будет приятно с утра стоять на линейке на чистой площади?"
    me "Слушай, я всё понимаю…{w} Но это слишком!"
    th "Не получилось...{w} Хотя, сейчас ведь действительно поздно."
    sl "Да, пожалуй, ты прав. Тогда…"
    "на меня нахлынула сонливость. Я потянулась и зевнула."
    sl "Пойдём спать! Утро вечера мудренее!"
    th "Убираться поздно, а вот спать рано? Да, Семёна иногда действительно трудно понять."
    me "А не рано…"
    sl "Не рано! Весь день потом проспишь."
    me "Тогда расскажи мне сказку на ночь!"
    "Третий раз за этот вечер он умудрялся удивлять меня."
    sl "Я знаю только те, что в книжках читала…{w} Ты наверняка их тоже знаешь."
    "Я слегка смутилась и решила перевести тему."
    sl "Пойдём?"
    "Я протянула ему руку"
    me "Я ещё немного посижу, воздухом подышу."
    me "Ты иди! Спокойной ночи!"
    stop music fadeout 3
    sl "Спокойной!"
    
#побег
    play ambience ambience_camp_center_evening fadein 2
    scene bg ext_dining_hall_away_night with dissolve
    "Я убежала. Мне было бы очень  неловко дальше находиться рядом с ним."
    "Не смотря на хорошее начало на дискотеке, Семёна потом словно подменили на кого-то очень унылого, а иногда даже несущего какой-то бред."
    th "Прямо как Лена, только ещё иногда говорит совершенно не в лад, невпопад."
    window hide
    scene black with dissolve
    pause (2)
    
#домик перед баней
    $ sunset_time()
    $ persistent.sprite_time = "sunset"
    play ambience ambience_int_cabin_night fadein 3
    scene bg int_house_of_sl_night_light with dissolve
    window show
    "В домике горел свет. Женя лежала и читала свою книгу."
    show mz normal glasses pioneer at center with dissolve
    mz "Ну как всё прошло?"
    "Спросила она скорее из вежливости, нежели из интереса."
    sl "Да неплохо."
    hide mz with dissolve
    "Бросила я. Женю такой ответ вполне устраивал и она продолжила чтение."
    "Я сняла платье и повесила его в шкаф. На глаза бросился пакетик с банными принадлежностями."
    th "Может сходить? Сегодня ведь было действительно жарко. Да, пожалуй, так и сделаю."
    "Надев свою форму и взяв так кстати попавшийся пакетик, я пошла к бане."
    window hide
    stop ambience fadeout 2
    scene black with dissolve
    pause (2)
    
#ночной лагерь
    $ night_time()
    $ persistent.sprite_time = "night"
    play ambience ambience_camp_center_night fadein 2
    scene bg ext_houses_night with dissolve
    window show
    "Лагерь был пуст и тих, даже окна во многих домиках были погашены."
    scene bg ext_square_night_party2 with dissolve
    "Я вышла на площадь. На ней так и оставили стоять всю аппаратуру и даже гирлянды на деревьях (благо, их хотя бы выключили)."
    th "Завтра это всё убирать придётся. Надеюсь, в этот раз смогут без меня, ведь я по сути-то ничего и не делала, а это плохо."
    window hide
    stop ambience fadeout 2
    scene black with dissolve
    pause (2)
    
#баня
    play music music_list["silhouette_in_sunset"] fadein 2
    if persistent.hentai:
        $ sunset_time()
        scene cg d3_sl_bathhouse_hk_alt:
            ypos -0.06
        with dissolve
    window show
    "К моему приходу баня уже была кем-то растоплена, мне оставалось только подкинуть пару дровишек."
    "Я неспешно помылась: ополоснула тело, голову, волосы..."
    if persistent.hentai:
        scene cg d3_sl_bathhouse with dissolve
    "В один момент мне, правда, показалось, что кто-то смотрит на меня, но я быстро отогнала эти мысли: кто же в здравом уме будет гулять по лесу в 12 часов ночи?"
    window hide
    pause (2)
    window show
    "Наконец я закончила, удовлетворённо вздохнула и направилась к двери."
    stop music fadeout 1
    if persistent.hentai:
        $ night_time()
    scene bg ext_bathhouse_night with dissolve
    play ambience ambience_forest_night fadein 3
    "Выйдя наружу, я на несколько секунд задержалась на крыльце: я так замечательно чувствовала себе после бани!"
    "Насладившись этим чувством вдоволь, я пошла обратно к лагерю."
    window hide
    stop ambience fadeout 2
    scene black with dissolve
    pause (2)
    
#площадь и Семён
    play ambience ambience_camp_center_night fadein 2
    scene bg ext_square_night_party2 with dissolve
    window show
    "Оказавшись на площади, я остановилась."
    th "Нет, всё-таки здесь можно прибраться и сейчас. По крайне мере, подмести точно. Не так уж тут и грязно, не запачкаюсь."
    "Около столба кто-то оставил метлу. Всё складывалось в пользу того, чтобы заняться уборкой прямо сейчас."
    "Я принялась подметать площадь."
    play sound_loop sfx_broom_sweep fadein 2
    window hide
    scene black with dissolve
    pause (2)
    scene bg ext_square_night_party2
    show pi normal pioneer at center
    with dissolve
    stop sound_loop
    window show
    "Разворачиваясь, чтобы переместиться одной части площади на другую, я чуть не столкнулась с Семёном. Его глаза были полузакрыты, словно он засыпал."
    "Метлу я убрала за спину, вдруг ещё натолкнётся."
    "Заметив меня, Семён всё-таки открыл глаза, но продолжал молчать."
    sl "Спать идёшь?"
    me "Ну... да..."
    show pi surprise pioneer at center with dspr
    "Тут он словно чего-то устыдился и отвёл взгляд."
    th "Что это с ним?"
    me "А ты?"
    sl "А я вот порядок навожу!"
    show pi normal pioneer at center with dspr
    "Я достала из-за спины метлу, убедившись, что он не спит. Напротив, он как-то странно посмотрел на меня..."
    sl "Что?"
    stop ambience fadeout 2
    me "Да ничего.{w} Думаешь, стоит после бани убираться?"
    play music music_list["no_tresspassing"] fadein 1
    "Семён решил не отступать от намеченного плана на вечер и продолжил меня удивлять. Это был уже четвёртый раз."
    sl "А откуда ты знаешь, что я в бане была?"
    show pi shocked pioneer at center with dspr
    "Он молчал. Я смотрела на него, в ожидании объяснений."
    me "Ну, у тебя волосы мокрые..."
    "Наконец вымолвил он."
    "Я слегка коснулась косы."
    sl "И правда. А ты тоже мыться пойдёшь?"
    "Я заметила схожий с моим пакет у него в руках."
    me "Я... эээ..."
    me "Да, да! Так что спокойной ночи!"
    hide pi with dspr
    "Он развернулся и быстрыми шагами направился к бане."
    stop music fadeout 2
    play ambience ambience_camp_center_night fadein 2
    th "Что это с ним на ночь глядя?{w} Может, Семён лунатик? А что, тогда он ещё больше похож на сову – ещё и живёт ночью."
    play sound_loop sfx_broom_sweep fadein 2
    "Я улыбнулась и продолжила подметать площадь."
    window hide
    scene black with dissolve
    pause (2)
    scene bg ext_square_night_party2 with dissolve
    stop sound_loop
    window show
    "Наконец, площадь была чиста. Я с чувством выполненного долга пошла домой."
    th "Не зря убралась!"
    window hide
    stop ambience fadeout 2
    scene black with dissolve
    pause (2)
    
#около домика перед сном (1)
    play ambience ambience_camp_center_night fadein 2
    play music music_list["tried_to_bring_it_back"] fadein 3
    scene bg ext_house_of_sl_night with dissolve
    window show
    "Когда я дошла до домика, то поняла, насколько устала за этот день."
    if sl_m_day1_help_od:
        "Уборка в лагере, подготовка к дискотеке, философские (а порой весьма странные) разговоры с Семёном, странности Оли... Всё это очень сильно вымотало меня.{w} Но нужно было ещё закончить дневник."
    else:
        "Уборка в лагере, подготовка к дискотеке, философские (а порой весьма странные) разговоры с Семёном... Всё это очень сильно вымотало меня.{w} Но нужно было ещё закончить дневник."
    th "Надеюсь, я не засну прямо на крыльце."
    window hide
    scene black with dissolve
    pause (1)
    scene bg ext_house_of_sl_night with dissolve
    window show
    "Как обычно прокравшись за дневником и положив пакет, я устроилась на крыльце и стала дописывать про события сегодняшнего дня:"
    
#блокнот (2)
    show bknt_w8 at truecenter with dissolve2
    play sound_loop pen_write
    show bknt_w9 at truecenter with dissolve2
    $ persistent.sl_m_bknt9 = True
    th "...А ещё я помогала готовить дискотеку. Она прошла на славу. Я танцевала с Семёном, это было... замечательно. Я ощущала себя такой счастливой..."
    th "Правда, потом он совершенно неучтиво убежал. Нашла я его на пляже, он был словно неживой. Говорил как робот, сделанный в кружке кибернетики. Правда, под конец он проснулся, но начал говорить что-то глупое и несвязное."
    th "Ох уж этот Семён... Как встретила его на уборке площади, так весь день дальше с ним и провела."
    stop sound_loop
    hide bknt_w8
    hide bknt_w9
    with dspr
    
#около домика перед сном (2)
    "На этом я решила поставить точку.{w} Удивительно, но спать совершенно не хотелось. Однако стрелки часов уже перевалили за полночь, поэтому мне нужно было ложиться спать, если я хочу завтра продолжить так же плодотворно работать."
    $ volume(0.1, "sound")
    play sound sfx_open_door_clubs
    stop ambience fadeout 2
    stop music fadeout 3
    scene bg int_house_of_sl_night with dissolve
    "Я легла в кровать, но сон никак не шёл. Ещё с полчаса я ворочалась, стараясь изгнать всякие мысли."
    "В основном они были о Семёне. Неудивительно, столько с ним общались за этот день."
    "Я твёрдо решила, что помогу ему начать меняться в лучшую сторону, и, наконец-то, заснула."
    window hide
    hide unblink
    show blink
    pause (1)
    scene black with dissolve
    hide blink
    $ volume(1.0, "sound")
    pause (3)
    $ persistent.sl_m_day3 = True
    #if sl_m_Full:
        #jump slavyana_mod__day4
    jump slavyana_mod__launcher0
    
#Сделано FireBoTer'ом